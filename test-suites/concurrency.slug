var {*} = import(
    "slug.std",
    "slug.test",
    "slug.time",
    "slug.channel",
)

@test
var test_spawn_returns_task_handles = fn() {
    var x = spawn {
        sleep(100)
        10 + 1
    }

    x /> type /> assertEqual(TASK_TYPE)
}

@test
var test_await_is_valid_in_functions = fn() {
    var x = spawn {
        sleep(100)
        10 + 1
    }

    x /> type /> assertEqual(TASK_TYPE)
    await(x) /> assertEqual(11)
}

@test
var test_nursery_actions_run_in_parallel = fn() {
    var c = delta(clock)
    var f = nursery fn() {
        var x = spawn {
            sleep(100)
            10 + 1
        }
        var y = spawn {
            sleep(100)
            10 * 10
        }
        var i = await(x, 150)
        var j = await(y, 150)
        i + j
    }
    f() /> assertEqual(111)
    c() /> assertLessThan(150)
}

@test
var test_completed_tasks_are_idempotent_and_cached = fn() {
    var c = delta(clock)
    var t = spawn {
        sleep(100)
        11
    }
    await(t) /> assertEqual(11)
    var time = c()
    time /> assertGreaterThanOrEqual(100) /> assertLessThan(150)
    assertEqual(await(t), 11)
    (c() - time) /> assertLessThan(5)
}

@test
var test_limit_1_forces_nursery_execution = fn() {
    var c = delta(clock)
    var f = nursery limit 1 fn() {
        var x = spawn {
            sleep(100)
            10 + 1
        }
        var y = spawn {
            sleep(100)
            10 * 10
        }
        var i = await(x, 250)
        var j = await(y, 250)
        i + j
    }
    f() /> assertEqual(111)
    c() /> assertGreaterThanOrEqual(200)
}

@test
var test_map_reduce_operate_in_the_same_thread_nursery = fn() {
    var c = delta(clock)
    var f = nursery fn(xs) {
        xs /> map(fn(x) {
            spawn {
                sleep(100)
                x
            }
        }) /> reduce(0, fn(a, h) {
            a + await(h)
        })
    }

    ([1,2,3]) /> f /> assertEqual(6)
    c() /> assertLessThan(200)
}

@test
var test_recur_does_not_join_threads = fn() {
    var c = delta(clock)
    var f = nursery fn(xs, acc = []) match {
        [[], ...] => acc
        [[h, ...t], ...] => recur(t, acc :+ spawn { sleep(100); h*2 })
    }
    f([1,2,3])
        /> reduce(0, fn(a, h) { a + await(h) })
        /> assertEqual(12)
    assertLessThan(c(), 150)
}

@test
var test_timeout_throws = fn() {
    var f = nursery fn() {
        var x = spawn {
            sleep(10)
        }
        var i = await(x, 1)
    }
    var r = runSafe(fn() { f() })
    r.error.type /> eq("Timeout")
    r.error.data.ms /> eq(1)
}

@test
var test_defer_onerror_catches_timeouts = fn() {
    var f = nursery limit 10 fn() {
        defer onerror(err) "pass"
        var x = spawn {
            sleep(10)
        }
        var i = await(x, 1)
    }
    f() /> eq("pass")
}

@test
var test_errors_cause_incomplete_tasks_to_cancel = fn() {
    var b  = nil
    var f = nursery limit 10 fn() {
        var a = spawn {
            sleep(5)
            throw "err"
        }
        b = spawn {
            sleep(10)
            10
        }
    }
    var throwError = runSafe(fn() { f() })
    var xldTask = runSafe(fn() { await(b) })
    
    throwError.error /> eq("err")

    xldTask.error["type"] /> eq("cancelled")
    xldTask.error["reason"] /> eq("sibling cancelled due to fail-fast")
}

@test
var test_errors_are_detected_during_await = fn() {
    var a = 0
    var f = nursery limit 10 fn() {
        var x = spawn {
            throw "err"
        }
        sleep(10)
        a = a + 1
        await(x)
        a = a + 1
    }
    var r = runSafe(fn() { f() })
    r.error /> eq("err")
    a /> eq(1)
}

@test
var test_errors_are_detected_during_nursery_exit = fn() {
    var f = nursery limit 10 fn() {
        spawn {
            throw "err"
        }
    }
    var r = runSafe(fn() { f() })
    r.error /> eq("err")
}


@test
var test_errors_should_honour_local_defer_onerror = fn() {
    var f = nursery limit 10 fn() {
        var a = nursery limit 10 fn() {
            defer onerror(e) "handled"
            spawn {
                throw "err"
            }
        }
        a()
    }
    f() /> println /> assertEqual("handled")
}

