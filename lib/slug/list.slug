var {*} = import (
    "slug.std",
    "slug.math"
);

@testWith(
    [[], []], true,
    [[1], []], false,
    [[], [1]], false,
    [[1], [2]], false,
    [[[1], [1]], [[1], [2]]], false,
    [[[1], [2]], [[1], [2]]], true
)
@export
var listEquals = fn(a, b, idx = 0) {
    if(idx >= len(a) && len(a) == len(b)) {
        true;
    } else if(type(a[idx]) == "LIST" && type(b[idx]) == "LIST" ) {
        listEquals(a[idx], b[idx]) && listEquals(a, b, idx + 1);
    } else if(a[idx] == b[idx]) {
        listEquals(a, b, idx + 1);
    } else {
        false;
    }
}

@testWith(
    [[1, 2], 2], 1,
    [[1, 2], 1], 0,
    [[1, 2], 9], -1
)
@export
var indexOfValue = fn(list, value, idx = 0) {
    if(idx > len(list)) {
        -1
    } else if (list[idx] == value) {
        idx
    } else {
        indexOfValue(list, value, idx + 1)
    }
}

@testWith(
    [[1,2,3], 2], [1,3],
    [[1,2,3], 5], [1,2,3]
)
@export
var removeValue = fn(list, value) {
    match indexOfValue(list, value) {
        i if i >= 0 => {
            list[:i] + list[i + 1:];
        }
        _ => list;
    }
}

@export
var shuffle = var fisherYatesShuffle = fn(list) {
    var _shuffle = fn(list, i) {
        if(i == 0) {
            list;
        } else {
            val j = rndRange(0, i)
            _shuffle(list.swap(i, j), i - 1)
        }
    };
    _shuffle(list, len(list) - 1);
}
