///
/// A terminal colours library inspired by the python `colorist` library.
///
/// See also:
///  - Colorist https://jakob-bagterp.github.io/colorist-for-python/
///  - the wikipedia page https://en.wikipedia.org/wiki/ANSI_escape_code#Colors
///

var {*} = import(
	"slug.std",
	"slug.string",
	"slug.list",
	"slug.sys",
	"slug.bytes",
	"slug.xkcd.colours",
)


val effectNames = ["bold", "dim", "underline", "blink", "reverse", "hide", ]
val effects = [1, 2, 4, 5, 7, 8, ]
val effectOffMod = 20
val offPostfix = "Off"

val colourNames = ["black", "red", "green", "yellow", "blue", "magenta", "cyan", "white", ]
val colours = [30, 31, 32, 33, 34, 35, 36, 37, ]
val bgMod = 10
val bgPrefix = "bg"
val brightMod = 60
val brightPrefix = "bright"
val bgBrightMod = 70
val bgBrightPrefix = "bgBright"

var checkTerminal = fn() {
	val [std, ...] = exec("tput colors", 10)
	match trim(std) {
		"0"   => 0
		"1"   => 1
		"2"   => 2
		"8"   => 8
		"16"  => 16
		"256" => 256
		_     => 0
	}
}

///
/// Parameters
/// - a list of code values
/// - the accumulator used to build the result
///
@testWith(
	[[31,1]], "31;1",
	[[0]], "0",
)
var combine = fn(@list i, acc = "") match {
	[[], ...] => acc
	[[h], ...] => acc + h
	[[h, ...t], ...] => recur(t, acc + h + ";" )
}

@testWith(
	[[31,1]], "\033[31;1m",
	[[0]], "\033[0m",
)
var ansiCode = fn(...i) {
	var s = i /> flatten /> combine
	"\033[{{s}}m"
}

@testWith(
	["s",[31,1]], "\033[31;1ms\033[0m",
)
var wrapWith = fn(@str str, ...s) {
	ansiCode(...s) + str + reset
}

@testWith(
	["red",""], "red",
	["red","bg"], "bgRed",
)
var styleName = fn(@str base, @str style) {
	if ( len(style) > 0 ) {
		style + toUpper(base[0]) + base[1:]
	} else {
		base
	}
}

var buildStyles = fn() {
	var stylesMap = {
		reset: [0],
	}
	var effectsMap = effectNames
		/> zip(effects)
		/> reduce(stylesMap, fn(a, b) {
			a /> put(b[0], [b[1]])
				/> put(b[0] + offPostfix, [b[1] + effectOffMod])
		})
	colourNames
		/> zip(colours)
		/> reduce(effectsMap, fn(a, b) {
			val base = b[0]
			val part = toUpper(base[0]) + base[1:]
			a /> put(base, [b[1]])
				/> put(bgPrefix + part, [b[1] + bgMod])
				/> put(brightPrefix + part, [b[1] + brightMod])
				/> put(bgBrightPrefix + part, [b[1] + bgBrightMod])
		})
}

var buildCodes = fn(@map styles, @str style = "") {
	var codeMap = {
		Off: ansiCode(styles.reset),
	}
	colourNames
		/> reduce(codeMap, fn(a, b) {
				val key = toUpper(b[0]) + b[1:]
				a /> put(key, ansiCode(styles[styleName(b, style)]))
			})
}

var buildEffects = fn(@map styles) {
	var effectMap = {
		Off: ansiCode(styles.reset),
	}
	effectNames
		/> reduce(effectMap, fn(a, b) {
				val key = toUpper(b[0]) + b[1:]
				a /> put(key, ansiCode(styles[b]))
					/> put(key + offPostfix, ansiCode(styles[b + offPostfix]))
			})
}

///
/// The number of supported colours reported by `tput colors`
///
@export val ColourSupport = checkTerminal()

///
/// Styles map, contains a list containing the ANSI escape code numbers for each style, 
/// map keys by colour name plus modifiers, e.g. black, brightBlack, bgBlack, bgBrightBlack
/// other keys include `reset` and the effects, e.g. blink and blinkOff
///
@export var Styles = buildStyles()
@export var Colour = buildCodes(Styles)
@export var BrightColour = buildCodes(Styles, brightPrefix)
@export var BgColour = buildCodes(Styles, bgPrefix)
@export var BgBrightColour = buildCodes(Styles, bgBrightPrefix)

///
/// A map of effects control codes.
///
/// The following keys are defined: bold, boldOff, dim, dimOff, underline, underlineOff,
/// blink, blinkOff, reverse, reverseOff, hide, hideOff
///
@export var Effects = buildEffects(Styles)

// reset styles
// -----------
///
/// A terminal control code the reset styles back to the default
///
@export var reset = ansiCode( Styles.reset )

// text colour
// -----------
@export var black = fn(@str str) { str /> wrapWith(Styles.black) }
@export var red = fn(@str str) { str /> wrapWith(Styles.red) }
@export var green = fn(@str str) { str /> wrapWith(Styles.green) }
@export var yellow = fn(@str str) { str /> wrapWith(Styles.yellow) }
@export var blue = fn(@str str) { str /> wrapWith(Styles.blue) }
@export var magenta = fn(@str str) { str /> wrapWith(Styles.magenta) }
@export var cyan = fn(@str str) { str /> wrapWith(Styles.cyan) }
@export var white = fn(@str str) { str /> wrapWith(Styles.white) }

@export var brightBlack = fn(@str str) { str /> wrapWith(Styles.brightBlack) }
@export var brightRed = fn(@str str) { str /> wrapWith(Styles.brightRed) }
@export var brightGreen = fn(@str str) { str /> wrapWith(Styles.brightGreen) }
@export var brightYellow = fn(@str str) { str /> wrapWith(Styles.brightYellow) }
@export var brightBlue = fn(@str str) { str /> wrapWith(Styles.brightBlue) }
@export var brightMagenta = fn(@str str) { str /> wrapWith(Styles.brightMagenta) }
@export var brightCyan = fn(@str str) { str /> wrapWith(Styles.brightCyan) }
@export var brightWhite = fn(@str str) { str /> wrapWith(Styles.brightWhite) }

// backgrounds
// -----------
@export var bgBlack = fn(@str str) { str /> wrapWith(Styles.bgBlack) }
@export var bgRed = fn(@str str) { str /> wrapWith(Styles.bgRed) }
@export var bgGreen = fn(@str str) { str /> wrapWith(Styles.bgGreen) }
@export var bgYellow = fn(@str str) { str /> wrapWith(Styles.bgYellow) }
@export var bgBlue = fn(@str str) { str /> wrapWith(Styles.bgBlue) }
@export var bgMagenta = fn(@str str) { str /> wrapWith(Styles.bgMagenta) }
@export var bgCyan = fn(@str str) { str /> wrapWith(Styles.bgCyan) }
@export var bgWhite = fn(@str str) { str /> wrapWith(Styles.bgWhite) }

@export var bgBrightBlack = fn(@str str) { str /> wrapWith(Styles.bgBrightBlack) }
@export var bgBrightRed = fn(@str str) { str /> wrapWith(Styles.bgBrightRed) }
@export var bgBrightGreen = fn(@str str) { str /> wrapWith(Styles.bgBrightGreen) }
@export var bgBrightYellow = fn(@str str) { str /> wrapWith(Styles.bgBrightYellow) }
@export var bgBrightBlue = fn(@str str) { str /> wrapWith(Styles.bgBrightBlue) }
@export var bgBrightMagenta = fn(@str str) { str /> wrapWith(Styles.bgBrightMagenta) }
@export var bgBrightCyan = fn(@str str) { str /> wrapWith(Styles.bgBrightCyan) }
@export var bgBrightWhite = fn(@str str) { str /> wrapWith(Styles.bgBrightWhite) }

// effects
// -----------
@export var effectBold = fn(@str str) { str /> wrapWith(Styles.bold) }
@export var effectDim = fn(@str str) { str /> wrapWith(Styles.dim) }
@export var effectUnderline = fn(@str str) { str /> wrapWith(Styles.underline) }
@export var effectBlink = fn(@str str) { str /> wrapWith(Styles.blink) }
@export var effectReverse = fn(@str str) { str /> wrapWith(Styles.reverse) }
@export var effectHide = fn(@str str) { str /> wrapWith(Styles.hide) }

// vga support (0 - 254)
// -----------
var vgaStyle = fn(code) { [38, 5, code] }
var vgaBgStyle = fn(code) { [48, 5, code] }
@export var vgaCode = fn(@num code) { code /> vgaStyle /> ansiCode }
@export var bgVgaCode = fn(@num code) { code /> vgaBgStyle /> ansiCode }
@export var vga = fn(@str str, @num code) { str /> wrapWith( vgaStyle(code) )}
@export var bgVga = fn(@str str, @num code) { str /> wrapWith( vgaBgStyle(code) )}

// rgb support
// -----------
var rgbStyle = fn(@num r, @num g, @num b) { [38, 2, r, g, b] }
var rgbBgStyle = fn(@num r, @num g, @num b) { [48, 2, r, g, b] }
@export var rgbCode = fn(@num r, @num g, @num b) { rgbStyle(r, g, b) /> ansiCode }
@export var bgRgbCode = fn(@num r, @num g, @num b) { rgbBgStyle(r, g, b) /> ansiCode }
@export var rgb = fn(@str str, @num r, @num g, @num b) { str /> wrapWith( rgbStyle(r, g, b) )}
@export var bgRgb = fn(@str str, @num r, @num g, @num b) { str /> wrapWith( rgbBgStyle(r, g, b) )}

// hex support (e.g. #fe7b7c)
// -----------
var hexStyle = fn(@str code) { code[1:] /> hexStrToBytes /> bytesToNumbers /> fn(v) { rgbStyle(...v) } }
var hexBgStyle = fn(@str code) { code[1:] /> hexStrToBytes /> bytesToNumbers /> fn(v) { rgbBgStyle(...v) } }
@export var hexCode = fn(@str code) { code /> hexStyle /> ansiCode }
@export var bgHexCode = fn(@str code) { code /> hexBgStyle /> ansiCode }
@export var hex = fn(@str str, @str code) { str /> wrapWith( hexStyle(code) )}
@export var bgHex = fn(@str str, @str code) { str /> wrapWith( hexBgStyle(code) )}

// xkcd color survey support (see https://xkcd.com/color/rgb/)
// -----------
var xkcdStyle = fn(@str name) { XkcdColourIndex[name] /> hexStyle }
var xkcdBgStyle = fn(@str name) { XkcdColourIndex[name] /> hexBgStyle }
@export var xkcdCode = fn(@str name) { name /> xkcdStyle /> ansiCode }
@export var bgXkcdCode = fn(@str name) { name /> xkcdBgStyle /> ansiCode }
@export var xkcd = fn(@str str, @str name) { str /> wrapWith( xkcdStyle(name) )}
@export var bgXkcd = fn(@str str, @str name) { str /> wrapWith( xkcdBgStyle(name) )}

