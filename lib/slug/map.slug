var {*} = import(
	"slug.std"
)

@testWith(
	[nil, nil], false,
	[{}, nil], false,
	[nil, {}], false,
	[{}, {}], true,
	[{k1:1}, {k1:1}], true,
	[{k1:1}, {}], false,
	[{k1:1}, {k2:2}], false
)
@export
var equals = fn(@map m1, @map m2) {
	if (m1 == nil || m2 == nil) {
		return false
	}
	var check = fn(ks) {
		match ks {
			[] => true;
			[k, ...t] if m1[k] == m2[k] => check(t);
			_ => false
		}
	}
	var k1 = m1.keys();
	var k2 = m2.keys();
	if(len(k1) != len(k2)) {
		false
	} else {
		check(k1 + k2);
	}
}

@testWith(
	[{k1:1}, {k2:2}], {k1:1, k2:2},
	[{k1:1}, {}], {k1:1},
	[{}, {k2:2}], {k2:2},
	[{k1:1}, {k1:9}], {k1:1} // s1 keys are kept on duplicates
)
@export
var union = fn(@map s1, @map s2) {
	s1.keys().reduce(s2, fn(s, k) {
		s.put(k, s1[k])
	})
}

@testWith(
	[{k1:1}, {k1:2}], {k1:1},
	[{k1:1}, {k2:2}], {},
	[{}, {k2:2}], {},
	[{k1:1}, {}], {}
)
@export
var intersect = fn(@map s1, @map s2) {
	s1.keys()
		.filter(fn(k) { s2[k] == nil })
		.reduce(s1, fn(s, k) { s.remove(k) })
}

@testWith(
	[{k1:1}, {k1:2}], {},
	[{k1:1, k2:1}, {k2:2}], {k1:1}
)
@export
var difference = fn(@map s1, @map s2) {
	s2.keys()
		.reduce(s1, fn(s, k) { s.remove(k) })
}

