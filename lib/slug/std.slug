//
// the slug standard library
// -------------------------

// return a string value indicating the type of `val`
@testWith(
	[nil], "NIL",
	[true], "BOOLEAN",
	[1], "NUMBER",
	[1.1], "NUMBER",
	["Hello Slug!"], "STRING",
	[[1, 2]], "LIST",
	[{key:"value"}], "MAP",
	[fn(a){}], "FUNCTION"
)
@export
foreign type = fn(val);

@testWith(
	["type"], true,
	["__not_defined__"], false
)
@export
foreign isDefined = fn(val);

@export
foreign printf = fn(str, ...args);

@export
foreign sprintf = fn(str, ...args);

// get the list of keys used a map
@testWith(
	[{}], [],
	[{k:1}], ["k"]
)
@export
foreign keys = fn(map);

// get a value from a map, nil if not present
@testWith(
	[{}, "k"], nil,
	[{k:1}, "k"], 1
)
@export
foreign get = fn(map, key);

@testWith(
	[{}, "k", "v"], {k:"v"}
)
@export
foreign put = fn(map, key, value);

@testWith(
	[{}, "k"], {},
	[{k:1}, "k"], {},
	[{k:1}, "j"], {k:1}
)
@export
foreign remove = fn(map, key);

@testWith(
	[[1,2,3], 1, 99], [1,99,3]
)
@export
foreign update = fn(list, index, value);

@testWith(
	[[1,2],0,1], [2,1]
)
@export
foreign swap = fn(list, index1, index2);

@testWith(
	[{k:1}, "k", fn(k, v) { v + 1 }], {k:2},
	[{}, "k", fn(k, v) { v == nil }], {k:true}
)
@export
var compute = fn(map, key, f) {
    map.put(key, f(key, map[key]));
}

@testWith(
	[[1,2], fn(n) {n * 2}], [2,4]
)
@export
var map = fn(vs, f, acc = []) {
	match vs {
		[h, ...t] => map(t, f, acc :+ h.f());
		[] => acc;
	}
}

@testWith(
	[[1,2,3,4], fn(v) { v % 2 == 0}], [2,4]
)
@export
var filter = fn(vs, f, acc = []) {
	match vs {
		[h, ...t] if h.f() => filter(t, f, acc :+ h);
		[h, ...t] => filter(t, f, acc);
		[] => acc;
	}
}

@testWith(
	[[], 0, fn(a,b) {a+b}], 0,
	[[1,2,3], 0, fn(a,b) {a+b}], 6,
	[[1,2,3], 9, fn(a,b) {a+b}], 15
)
@export
var reduce = fn(vs, v, f) {
	match vs {
		[h, ...t] => reduce(t, f(v, h), f);
		[] => v;
	}
}

@export
var counter = fn(start = 0) {
    var count = start - 1;
    fn() {
        count = count + 1;
    };
}

@testWith(
	[["a", "b"], fn(){1}], [["a", 1], ["b", 1]]
)
@export
var zipWith = fn(lst, f) {
	lst.map(fn(v) { [v, f()] });
}

@testWith(
	[[]], [],
	[["a", "b"]], [["a", 0], ["b", 1]]
)
@export
var zipWithIndex = fn(lst) {
	lst.zipWith(counter(0));
}

@testWith(
	[[], []], [],
	[[1], []], [],
	[[], [1]], [],
	[[1], [2]], [[1, 2]]
)
@export
var zip = fn(lst1, lst2, acc = []) {
	if (len(lst1) == 0 || len(lst2) == 0) {
		acc
	} else {
		zip(lst1[1:], lst2[1:], acc :+ [lst1[0], lst2[0]])
	}
}

@testWith(
	[1, 1], 0,
	[1, 2], 1,
	[2, 1], 0
)
@export
var zeroIfAbove = fn(a, b) {
	if ( a < b ) {
		a
	} else {
		0
	}
}
