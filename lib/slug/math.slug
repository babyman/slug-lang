var {*} = import(
	"slug.std",
)


@testWith(
	[1, 2, 3], 3,
	[7, 9, 8], 9,
	[4, 5, 3], 5
)
@export
var max = fn(@num a, ...b) {
	val _max = fn(x, y) {if (x > y) {x} else {y}}
	searchList(b, a, _max)
}


@export
var max = fn(@num a, @num b) {
	if (a > b) { a } else { b }
}


@testWith(
	[1, 2, 3], 1,
	[4, 3, 5], 3,
	[6, 5, 4], 4
)
@export
var min = fn(@num a, ...b) {
	val _min = fn(x, y) {if (x < y) {x} else {y}}
	searchList(b, a, _min)
}


@export
var clampZero = fn(@num n) {
	if (n < 0) { 0 } else { n }
}


// floor returns the greatest integer less than or equal to n.
@testWith(
	[1.0], 1,
	[1.2], 1,
	[-1.2], -2,
)
@export
foreign floor = fn(@num n)


// ceil returns the least integer greater than or equal to n.
@testWith(
	[1.0], 1,
	[1.2], 2,
	[-1.2], -1,
)
@export
foreign ceil = fn(@num n)


// sqrt returns the square root of n.
// For n < 0, it returns NaN.
@testWith(
	[0], 0,
	[4], 2,
	[9], 3,
)
@export
foreign sqrt = fn(@num n)


// random_range generates a random integer in the range [min, max] (exclusive).
// min: Minimum value of the range (integer).
// max: Maximum value of the range (integer).
// Returns: a random integer in the specified range.
@export
foreign rndRange = fn(@num min, @num max)


@export
var mean = fn(xs) match {
	[] => 0
	_ => {
		(xs /> reduce(0, fn(a, x) { a + x })) / (len(xs))
	}
}


@export
var stdev = fn(xs, mean) match {
	[] => 0
	[_] => 0
	_ => {
		val n = len(xs)
		val acc = xs /> reduce(0, fn(a, x) {
			val d = x - mean
			a + (d * d)
		})
		val v = acc / (n - 1)
		sqrt(if (v < 0) { 0 } else { v })
	}
}


/// percentileSorted: xs must be ascending
@export
var percentileSorted = fn(xs, p) match {
	[[], ...] => 0
	[[x], ...] => x
	_ => {
		val n = len(xs)
		val pos = (p / 100) * (n - 1)
		val lo = floor(pos)
		val hi = ceil(pos)

		if (lo == hi) {
			xs[lo]
		} else {
			val frac = pos - lo
			xs[lo] + (xs[hi] - xs[lo]) * frac
		}
	}
}


var searchList = fn(vals, result, f, idx = 0) {
	if(len(vals) > idx) {
		recur(vals, f(vals[idx], result), f, idx + 1)
	} else {
		result
	}
}
