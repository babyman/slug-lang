var {*} = import(
	"slug.std"
)

var searchList = fn(vals, result, f, idx = 0) {
	if(len(vals) > idx) {
		recur(vals, f(vals[idx], result), f, idx + 1)
	} else {
		result
	}
}

@testWith(
	[1, 2, 3], 3,
	[7, 9, 8], 9,
	[4, 5, 3], 5
)
@export
var max = fn(a, ...b) {
	val _max = fn(x, y) {if (x > y) {x} else {y}}
	searchList(b, a, _max)
}

@testWith(
	[1, 2, 3], 1,
	[4, 3, 5], 3,
	[6, 5, 4], 4
)
@export
var min = fn(a, ...b) {
	val _min = fn(x, y) {if (x < y) {x} else {y}}
	searchList(b, a, _min)
}

// floor returns the greatest integer less than or equal to n.
@testWith(
	[1.0], 1,
	[1.2], 1,
	[-1.2], -2,
)
@export
foreign floor = fn(n)

// ceil returns the least integer greater than or equal to n.
@testWith(
	[1.0], 1,
	[1.2], 2,
	[-1.2], -1,
)
@export
foreign ceil = fn(n)

// sqrt returns the square root of n.
// For n < 0, it returns NaN.
@testWith(
	[0], 0,
	[4], 2,
	[9], 3,
)
@export
foreign sqrt = fn(n)

// random_range generates a random integer in the range [min, max] (exclusive).
// min: Minimum value of the range (integer).
// max: Maximum value of the range (integer).
// Returns: a random integer in the specified range.
@export
foreign rndRange = fn(min, max)
