var {*} = import(
    "slug.std", 
    "slug.actor",
    "slug.time",
    "slug.list"
);

val __TRACE = 0;
val __DEBUG = 1;
val __INFO = 2;
val __WARN = 3;
val __ERROR = 4;
val __NONE = 5;

val __LEVELS = ["trace", "debug", "info", "warn", "error", "none"];
val __COLOURS = ["\033[90m", "\033[36m", "\033[32m", "\033[33m", "\033[31m"];
val __RESET_COLOR = "\033[0m";

var _logger = fn(logLevel = __NONE) {
    match receive() {
        {tag:"configure", level} => _logger(level)
        {tag:"log", level, ...l} if level >= logLevel => {
                val tag = sprintf("%s%-5s%s", __COLOURS[level], __LEVELS[level], __RESET_COLOR);
                var msg = sprintf(l.message, ...l.args);
                if (msg[-1] != "\n") {
                    msg = msg + "\n";
                }
                printf("%s [ %s ] %s", fmtClock(l.time, "2006-01-02 15:04:05.000"), tag, msg);
            }
    }
    _logger(logLevel);
}

// create a mailbox and bind the logging function to it
var _log_actor = mailbox().bindActor(_logger);

var configureLogging = fn(level) {
    val lvl = __LEVELS.indexOfValue(level);
    _log_actor.send({ tag:"configure", level: lvl })
}

var _log = fn(level, message, args) {
    _log_actor.send({ tag: "log", time: clock(), level: level, message: message, args: args })
}

var logTrace = fn(message, ...args) { _log(__TRACE, message, args) }
var logDebug = fn(message, ...args) { _log(__DEBUG, message, args) }
var logInfo = fn(message, ...args) { _log(__INFO, message, args) }
var logWarn = fn(message, ...args) { _log(__WARN, message, args) }
var logError = fn(message, ...args) { _log(__ERROR, message, args) }
var logNone = fn(message, ...args) { _log(__NONE, message, args) }
