var {*} = import(
    "slug.std"
);
var {equals} = import(
    "slug.list"
);

@export
var assert = fn(a, msg = nil) {
    if ( a.type() == BOOLEAN_TYPE && !a ) {
        if ( !msg ) {
            msg = "{{a}} ({{a.type()}}) is not true";
        }
        throw AssertionError({msg: msg});
    }
}

@export
var assertTrue = assert;

@export
var assertFalse = fn(a, msg = nil) {
    assert( !a, msg);
}


@export
var assertEqual = fn(a, b, msg = nil) {
    if ( a != b ) {
        if ( !msg ) {
            msg = "{{a}} is not equal to {{b}}";
        }
        throw AssertionError({msg: msg});
    }
}


@export
var assertEqual = fn(@list a, @list b, msg = nil) {
    if ( !(a == b || equals(a, b)) ) {
        if ( !msg ) {
            msg = "List '{{a}}' is not equal to '{{b}}'";
        }
        throw AssertionError({msg: msg});
    }
}


@export
var fail = fn(msg = nil) {
    if ( !msg ) {
        msg = "Test failed!";
    }
    throw AssertionError({msg: msg});
}
