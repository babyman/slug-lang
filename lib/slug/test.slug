var {*} = import(
    "slug.std"
);

/// runSafe executes f() and returns a map:
///   {"ok": value} on normal return
///   {"error": thrownValue, "trace": stacktrace(thrownValue)} on throw
///
/// Implementation uses `defer onerror` to intercept thrown payload.
@export
var runSafe = fn(@fun f) {
    defer onerror(err) {
        { error: err, trace: stacktrace(err) }
    }
    var v = f()
    { ok: v }
}

@export
var isAssertError = fn(v) {
    v /> type() == MAP_TYPE && v.type == "AssertionError"
}

@export
var assertThrows = fn(@fun f, expected, @str msg = nil) {
    var r = runSafe(f)
    if (r.error == expected) {
        true
    } else {
        if ( !msg ) {
            if ( r.error ) {
                msg = "Caught error '{{ r.error }}' is not equal to '{{expected}}'";
            } else {
                msg = "No error was thrown";
            }
        }
        throw {type: "AssertionError", msg: msg}
    }
}

@export
var assertErrorType = fn(@fun f, @str expected, @str msg = nil) {
    var r = runSafe(f)
    if (r.error /> type() == expected) {
        true
    } else {
        if ( !msg ) {
            if ( r.error ) {
                msg = "Caught error of type '{{ r.error /> type() }}' type is not equal to '{{expected}}'";
            } else {
                msg = "No error was thrown";
            }
        }
        throw {type: "AssertionError", msg: msg}
    }
}

@export
var assert = fn(a, msg = nil) {
    if ( a /> type() == BOOLEAN_TYPE && !a ) {
        if ( !msg ) {
            msg = "{{a}} ({{a /> type()}}) is not true";
        }
        throw {type: "AssertionError", msg: msg}
    }
}

@export
var assertTrue = assert;

@export
var assertFalse = fn(a, msg = nil) {
    assert( !a, msg);
}

@export
var assertEqual = fn(value, expected, msg = nil) {
    var t = value /> type()

    var same =
        (t == LIST_TYPE   && equals(value, expected)) ||
        (t == MAP_TYPE    && equals(value, expected)) ||
        (value == expected)

    if (!same) {
        if (!msg) {
            msg = "'{{value}}' is not equal to '{{expected}}'"
        }
        throw {type: "AssertionError", msg: msg}
    }
    value
}


@export
var assertNotEqual = fn(value, expected, msg = nil) {
    if (value == expected || equals(value, expected)) {
        if (!msg) {
            msg = "Expected '{{value}}' to NOT equal '{{expected}}'"
        }
        throw {type: "AssertionError", msg: msg}
    }
    value
}

@export
var assertNil = fn(value, msg = nil) {
    if (value != nil) {
        if (!msg) {
            msg = "Expected nil but got '{{value}}'"
        }
        throw {type: "AssertionError", msg: msg}
    }
}

@export
var assertNotNil = fn(value, msg = nil) {
    if (value == nil) {
        if (!msg) {
            msg = "Expected non-nil"
        }
        throw {type: "AssertionError", msg: msg}
    }
}

@export
var fail = fn(msg = nil) {
    if ( !msg ) {
        msg = "Test failed!";
    }
    throw {type: "AssertionError", msg: msg}
}

@export var eq = assertEqual
@export var throws = assertThrows
@export var ok = assertTrue
@export var not = assertFalse
