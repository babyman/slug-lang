var {*} = import(
    "slug.io.fs",
    "slug.std",
    "slug.actor",
    "slug.test",
);

var filename = "__slug_test.txt";
var hello = "Hello Slug!\n";
var goodbye = "Goodbye Slug!\n";

// read / write / append entire file content
// -----------------------------------------
hello /> writeFile(filename);
goodbye /> appendFile(filename);
readFile(filename) /> assertEqual(hello + goodbye);

// inspect the file
// ----------------

var info = filename /> info();
info.name /> assertEqual(filename);
info.isDir /> assertFalse();
(info.mode > 0) /> assertTrue();
(info.modTime > 0) /> assertTrue();
(info.size > 0) /> assertTrue();

filename /> isDir() /> assertFalse();

filename /> exists() /> assertTrue();

// remove the file
// ---------------
filename /> rm();

// read / write / append file with handle
// --------------------------------------
withReplyActor(fn(reply) {
    var fileOut = filename /> openFile(reply, WRITE_MODE)
    fileOut /> write(reply, hello) /> receiveFrom(1000)
    fileOut /> closeFile(reply)
})

withReplyActor(fn(reply) {
    var fileAppend = filename /> openFile(reply, APPEND_MODE)
    fileAppend /> write(reply, goodbye) /> receiveFrom(1000)
    fileAppend /> closeFile(reply)
})


withReplyActor(fn(reply) {
    var fileIn = filename /> openFile(reply, READ_MODE)
    defer filename /> rm()
    defer fileIn /> closeFile(reply)
    //defer fileIn /> println("<<<< file defer")

    //fileIn /> println("<<<< file about to read")
    fileIn /> readLines(reply) /> len /> assertEqual(2);
    //fileIn /> println("<<<< file reading");
});


// ls
// ---------

("./" /> ls /> len > 0) /> assertTrue()
