var {*} = import(
    "slug.string",
    "slug.test"
);

// String escape sequences:
//
// - `\n` -> newline
// - `\t` -> tab
// - `\\` -> backslash
// - `\"` -> double quote
//


// String functions
// -----------------------

var s = "Hello Slug!";

var ms = """
Hello
Slug!
""";

ms /> assertEqual("Hello\nSlug!");


"  " /> assertEqual("  ");

"  " /> trim() /> assertEqual("");

" é " /> trim() /> assertEqual("é");

// length
// -----------------------

// utf-8 string length
len("éééé") /> assertEqual(4);


assertEqual(s /> len(), 11);


// contains
// -----------------------
assert(s /> contains("Slug"), "Expected to contain Slug");
assert(!s /> contains("slug"), "Contains is case sensitive");


// startsWith
// -----------------------
assert(s /> startsWith("Hell"), "Does not start with Hell");
assert(!s /> startsWith("A"), "Starts with A");


// endsWith
// -----------------------
assert(s /> endsWith("Slug!"), "does not end with Slug!");
assert(!s /> endsWith("aaa"), "ends with aaa");


// indexOf
// -----------------------
assertEqual(s /> indexOf("aaa"), -1, "not found");
assertEqual(s /> indexOf("H"), 0, "H found at index 0");
assertEqual(s /> indexOf("Slug"), 6, "Slug starts at index 6");

// indexOf using a start index
// -----------------------
assertEqual(s /> indexOf("H", 1), -1, "H found at index 0");


// trim
// -----------------------
assertEqual("  foo  " /> trim(), "foo");

// Multiple strings to repeat
// -----------------------
("é" * 4) /> assertEqual("éééé");
