var {*} = import(
	"slug.std",
	"slug.sys",
	"slug.mustache",
	"slug.json",
	"slug.io.fs",
	"slug.test",
	"slug.term.colour",
)

val Fail = BrightColour.Red
val Pass = Colour.Green
val Reset = reset
val specDir = 'mustache/spec-1.4.3/specs'

var run = fn(t) {
	defer onerror(err) {
		println("  [{{Fail}}FAIL{{Reset}}] {{t.name}}: {{t.desc}} / {{err.msg}}")
		[0, 1]
	}
	render(t.template, t.data, t.partials) /> assertEqual(t.expected)
	println("  [{{Pass}}PASS{{Reset}}] {{t.name}}")
	[1, 0]
}

var score = fn(a, b) { [a[0] + b[0], a[1] + b[1]] }

var runAll = fn(xs, acc = []) match {
	[[], ...]        => acc
	[[x, ...t], ...] => {
		println(x)
		println("====================")
		var result = (specDir + x)
			/> readFile 
			/> decode
			/> fn(v) { v.tests /> map(run) /> reduce([0,0,0], score) }
		recur(t, acc :+ result)
	}
}

[
	// '/~dynamic-names.json',
	// '/~inheritance.json',
	// '/~lambdas.json',
	'/comments.json', 
	'/delimiters.json',
	'/interpolation.json',
	'/inverted.json',
	'/partials.json',
	'/sections.json',
] /> runAll /> reduce([0,0], score) /> println /> match {
	[_, 0] => println("All tests passed!")
	_      => {
		println("Some tests failed!")
		exit(1)
	}
}
