import slug.test.*;

//
// matching on list values
// -----------------------

var f1 = fn(x) {
    match x {
        [] => "empty list";
        [a] => "list of one";
        [a, b] => "list of two";
        [a, b, c] => "list of three";
        _ => "default";
    }
};

[].f1().assertEqual("empty list");

[1].f1().assertEqual("list of one");

[1, 2].f1().assertEqual("list of two");

[1, 2, 3].f1().assertEqual("list of three");

//
// matching list wildcards
// -----------------------

var f2 = fn(x) {
    match x {
        [a, b, _] => "list of 3";
        [a, _] => "list of 2";
        [_] => "list of 1";
        [] => "empty list";
        _ => "default";
    }
};

[1, 2, 3, 4, 5].f2().assertEqual("default");

[1, 2, 3].f2().assertEqual("list of 3");

[1].f2().assertEqual("list of 1");

[].f2().assertEqual("empty list");


//
// matching list spreads
// ---------------------

var f2 = fn(x) {
    match x {
        [4, a, ...] => "" + a;
        [h, _, ...t] => "list of two + " + len(t);
        [_, ...] => "list of one+";
        [...] => "any list";
        _ => "default";
    }
};

[1, 2].f2().assertEqual("list of two + 0");

[1, 2, 3, 4].f2().assertEqual("list of two + 2");

[1].f2().assertEqual("list of one+");

[4, 6, 1].f2().assertEqual("6");

[].f2().assertEqual("any list");


