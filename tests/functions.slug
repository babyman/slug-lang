var {*} = import(
    // "slug.std",
    "slug.test"
);


// declare a function
@export
var sqr = fn(a) { a * a }

@export
var sum = fn(ns, acc = 0) {
    match ns {
        [h, ...t] => sum(t, acc + h);
        [] => acc;
    }
}

// function chaining
// -----------------------
// function chaining works here because the code is extrapolated by the parser into:
//
// var n = sqr(sqr(2))
//
var out = 2 /> sqr() /> sqr();
assert(out == 16, "got:" + out);

//
// default parameter value
// -----------------------
var f1 = fn(a, b = 1) { a + b }
var out = 1 /> f1();
assert(out == 2, "expected 2, got " + out);
var out = 1 /> f1(10);
assert(out == 11, "expected 11, got " + out)


// variadic function example
// -------------------------
var f2 = fn(a, ...b) { a + b /> len() }
var out = 1 /> f2(0, 0, 0);
assert(out == 4, "expected 4, got " + out);

// variadic spread example
// -----------------------

var f3 = fn(...args) {
    args;
}

f3(1, 2, 3) /> assertEqual([1, 2, 3]);
f3(...[1 ,2]) /> assertEqual([1, 2]);
f3(0, ...[1 ,2]) /> assertEqual([0, 1, 2]);
f3(...[1 ,2], 9) /> assertEqual([1, 2, 9]);
f3(0, ...[1 ,2], 9) /> assertEqual([0, 1, 2, 9]);
f3(...[1 ,2] :+ 3) /> assertEqual([1, 2, 3]);
f3(0,...[1 ,2] :+ 3, 9) /> assertEqual([0, 1, 2, 3, 9]);
