var {*} = import(
    // "slug.std",
    "slug.test"
)


// declare a function and mark it as exported
// this will allow it to be imported and used in other modules
@export
var sqr = fn(a) { a * a }

@export
var sum = fn(ns, acc = 0) {
    match ns {
        [h, ...t] => sum(t, acc + h)
        [] => acc
    }
}

// function chaining
// -----------------------
// function chaining works here because the code is extrapolated by the parser into:
//
// var n = sqr(sqr(2))
//
var out = 2 /> sqr() /> sqr()
assert(out == 16, "got:" + out)

//
// default parameter value
// -----------------------
var f1 = fn(a, b = 1, c = 1) { a + b + c }
1 /> f1() /> assertEqual(3)
1 /> f1(10) /> assertEqual(12)
1 /> f1(10, c = 2) /> assertEqual(13)
f1(a = 10, c = 2) /> assertEqual(13)
f1(c = 10, a = 2) /> assertEqual(13)


// variadic function example
// -------------------------
var f2 = fn(a, ...b) { (a +: b) /> len }
f2(1, 0, 0, 0) /> assertEqual(4)
f2(a = 1, b = [0, 0, 0]) /> assertEqual(4)
f2(b = [0, 0, 0], a = 1) /> assertEqual(4)


// variadic spread example
// -----------------------

var f3 = fn(...args) {
    args
}

f3(1, 2, 3) /> assertEqual([1, 2, 3])
f3(...[1 ,2]) /> assertEqual([1, 2])
f3(0, ...[1 ,2]) /> assertEqual([0, 1, 2])
f3(...[1 ,2], 9) /> assertEqual([1, 2, 9])
f3(0, ...[1 ,2], 9) /> assertEqual([0, 1, 2, 9])
f3(...[1 ,2] :+ 3) /> assertEqual([1, 2, 3])
f3(0,...[1 ,2] :+ 3, 9) /> assertEqual([0, 1, 2, 3, 9])
