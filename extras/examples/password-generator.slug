#!/usr/bin/env slug

var {*} = import(
	"slug.cli",
	"slug.std",
	"slug.sys",
	"slug.list",
)

val Version = "1.0"

var args = argm()
var opts = args.options
var params = args.positional

var help = opts.h || opts.help
var version = opts.v || opts.version
var length = opt(opts.l, opts.length, 25)
var count = opt(opts.c, opts.count, 5)
var letters = opt(opts.letters, "abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ")
var numbers = opt(opts.numbers, "1234567890")
var symbols = opt(opts.symbols, "!@#$^&*:;,./?")


if (version) {
"Slug Password Generator version {{Version}}" /> println
exit(0)
} else if (help) {
"""
Slug Password Generator (SPG)
=============================

Welcome to the Slug password generator version {{Version}}.

usage: password-generator [-v | --version] [-h | --help] [-l | --length <number>] [-c | --count <number>]
                          [--letters <string>] [--numbers <string>] [--symbols <string>]

  -v | --version         print version information
  -h | --help            print this help message
  -l | --length <number> the length of password to generate
  -c | --count <number>  the number of passwords to generate
  --letters <string>     the letter characters to use
  --numbers <string>     the number characters to use
  --symbols <string>     the symbols to use

""" /> println
exit(0)
}


val generatePassword = fn(max = 0, chars, acc = "") {
	if (len(acc) >= max) {
		acc
	} else {
		generatePassword(max, chars, acc + (chars /> shuffle())[0])
	}
}

val chars = (letters + numbers + symbols) /> asList()

range(1, count + 1)
	 /> map(fn(i) {i + ". " + generatePassword(length, chars)})
	 /> reduce("Passwords generated:\n\n", fn(a, b) {a + b + "\n"})
	 /> println

