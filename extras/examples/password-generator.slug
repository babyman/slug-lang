#!/usr/bin/env slug

var {*} = import(
	"slug.cli",
	"slug.std",
	"slug.sys",
	"slug.list",
)

val Version = "1.0"

var args = argm()
var help = args.options.h || args.options.help
var version = args.options.v || args.options.version
var length = option(args.options.l, args.options.length, 25)
var count = option(args.options.c, args.options.count, 5)
var letters = option(args.options.letters, "abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ")
var numbers = option(args.options.numbers, "1234567890")
var symbols = option(args.options.symbols, "!@#$^&*:;,./?")

var params = args.positional

if (version) {
	"Slug Password Generator version {{Version}}" /> println
	exit(0)
} else if (help) {
"""
Slug Password Generator (SPG)
=============================

Welcome to the Slug password generator version {{Version}}.

""" /> println
	exit(0)
}


val generatePassword = fn(max = 0, chars, acc = "") {
	if (len(acc) >= max) {
		acc
	} else {
		generatePassword(max, chars, acc + (chars /> shuffle())[0])
	}
}

val chars = (letters + numbers + symbols) /> asList()

range(1, count + 1)
	 /> map(fn(i) {i + ". " + generatePassword(length, chars)})
	 /> reduce("Passwords generated:\n\n", fn(a, b) {a + b + "\n"})
	 /> println

